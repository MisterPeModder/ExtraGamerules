plugins {
	id 'fabric-loom' version '0.2.0-SNAPSHOT'
	id 'com.matthewprenger.cursegradle' version "1.1.2"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "extra-gamerules"
version = "1.2.0"
def mcVersion = "19w02a"

minecraft {
}

dependencies {
	minecraft "com.mojang:minecraft:$mcVersion"
	mappings "net.fabricmc:yarn:$mcVersion.13"
	modCompile "net.fabricmc:fabric-loader:0.3.2.92"
	modCompile "net.fabricmc:fabric:0.1.4.71"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

String parseChangelog(String path) {
	def file = new File(path)
	if (!file.exists())
		return '(no changelog)'
	String changelog = ""
	boolean end = false
	int line = 0
	file.eachLine {
		if (end || it == null)
			return
		++line
		if (!it.startsWith('+=+=+'))
			changelog += "$it\n"
		else if (line > 3)
			end = true
	}
	return changelog
}

curseforge {
	if (project.hasProperty('curse_api_key')){
		apiKey = project.getProperty('curse_api_key')
	}
	project {
		id = '310323'
		changelog = parseChangelog('changelog.txt')
		releaseType = 'release'
		addGameVersion '1.14-Snapshot'
		mainArtifact(jar) {
			displayName = "ExtraGamerules-${mcVersion}-${project.version}"
		}
		relations {
      requiredDependency 'fabric'
    }
	}
	options {
		forgeGradleIntegration = false
	}
}

afterEvaluate {
	// Manually set dependsOn because curseforge doesn't set one.
  tasks.curseforge310323.dependsOn remapJar
}